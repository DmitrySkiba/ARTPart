/*
 * Copyright (C) 2015 Dmitry Skiba
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.commons.io.FilenameUtils

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'commons-io:commons-io:2.4'
    }
}

// Load build_props.json
apply from: 'settings.gradle'

ext {
    buildRoot = new File(buildProps.build_root, 'gradle')

    // TODO: rename automatically
    platformRoot = new File(buildProps.platform_root)
    gradleRoot = new File(buildProps.gradle_root)
    outRoot = new File(buildProps.out_root)
    productRoot = new File(buildProps.product_root)

    androidFSRoot = new File(buildProps.android_fs_root)
    androidRootPath = new File(buildProps.android_root_path)
    androidFrameworkPath = new File(buildProps.android_framework_path)
    androidDataPath = new File(buildProps.android_data_path)

    bootArtFile = new File(buildProps.boot_art_file)
    bootOatFile = new File(buildProps.boot_oat_file)
    dex2oatFile = new File(buildProps.dex2oat_file)
    jarjarTool = new File(buildProps.jarjar_tool)

    instructionSet = buildProps.instruction_set
    bootOatBase = buildProps.boot_oat_base
}

// Utilities

def dexPathFromJar(File jarFile) {
    return new File(androidFrameworkPath, jarFile.getName())
}

class Dex extends DefaultTask {
    Boolean coreLibrary = false

    @InputFile
    File jarFile

    @OutputFile
    File dexFile

    @TaskAction
    def dex() {
        project.exec {
            executable = project.buildProps.BUILD_ANDROID_DX

            args '--dex'
            if (coreLibrary) {
                args '--core-library'
            }
            args "--output=$dexFile", jarFile
        }
    }
}
ext.Dex = Dex

// Tasks

configurations {
    allDexedJars
}

project.subprojects { subproject ->
    buildDir = new File(buildRoot, name)

    plugins.withType(JavaPlugin) {
        // TODO: find a way to check if a subproject has 'dexedJar' configuration
        //       instead of creating it for all subprojects
        subproject.configurations.create('dexedJar')
        rootProject.dependencies {
            allDexedJars project(path: subproject.path, configuration: 'dexedJar')
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

// TODO: extract part common with HelloWorld:oat into a class
task bootOat {
    inputs.file dex2oatFile
    inputs.file configurations.allDexedJars
    outputs.file bootArtFile
    outputs.file bootOatFile

    doLast {
        project.exec {
            workingDir = androidFSRoot
            executable = relativize(androidFSRoot, dex2oatFile)
            args "--android-root=${relativize(androidFSRoot, androidRootPath)}"
            args '--runtime-arg', '-Xms64m'
            args '--runtime-arg', '-Xmx64m'
            args '--runtime-arg', '-XX:DisableHSpaceCompactForOOM'
            args "--instruction-set=${instructionSet}"

            configurations.allDexedJars.files.each { dexedJar ->
                args "--dex-file=${relativize(androidFSRoot, dexedJar)}"
            }
            args "--base=$bootOatBase"
            args "--image-classes=$platformRoot/frameworks/base/preloaded-classes"
            args "--image=${relativize(androidFSRoot, bootArtFile)}"
            args "--oat-file=${relativize(androidFSRoot, bootOatFile)}"
        }
    }
}

task clean(type: Delete) {
    delete bootOat.outputs
}
